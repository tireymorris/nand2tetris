// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // if zx, feed false, else x
    // if nx, feed notx, else x
    Mux16(a=x, b=false, sel=zx, out=preXVal);
    Not16(in=preXVal, out=notX);
    Mux16(a=preXVal, b=notX, sel=nx, out=postXVal);

    // y = zy ? 0 : y
    // ny = noty ? !y : y
    Mux16(a=y, b=false, sel=zy, out=preYVal);
    Not16(in=preYVal, out=notY);
    Mux16(a=preYVal, b=notY, sel=ny, out=postYVal);

    // Calculate output = f ? And(x,y) : Add(x,y)
    And16(a=postXVal, b=postYVal, out=and);
    Add16(a=postXVal, b=postYVal, out=add);
    Mux16(a=and, b=add, sel=f, out=outVal);

    // if no, negate the output
    // ng = out[15]
    Not16(in=outVal, out=notOutVal);
    Mux16(a=outVal, b=notOutVal, sel=no, out[15]=ng, out[0..7]=part1, out[8..15]=part2, out=out);

    // calculate zr, if result == zero
    Or8Way(in=part1, out=zr1);
    Or8Way(in=part2, out=zr2);
    Or(a=zr1, b=zr2, out=nzr);
    Not(in=nzr, out=zr);
}